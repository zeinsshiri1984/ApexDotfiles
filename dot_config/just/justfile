set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

default:
    @just --list

setup:
    @brew bundle --no-lock --file="${XDG_CONFIG_HOME:-$HOME/.config}/homebrew/Brewfile"
    @mise install
    @mise reshim
    @hx --grammar fetch || true
    @hx --grammar build || true

# --- Update & Maintenance ---

# Update all tools and configs (safe, no heavy cleaning)
update:
    @~/.local/bin/update-all

# --- Cleaning Tasks (Manual Trigger) ---

# Clean Docker/Podman (prune all unused images/volumes)
clean-docker:
    @echo "Cleaning container resources..."
    @if command -v podman &> /dev/null; then \
        podman system prune -a -f --volumes; \
    elif command -v docker &> /dev/null; then \
        docker system prune -a -f --volumes; \
    else \
        echo "No container engine found."; \
    fi

# Clean language caches (Go, Node, Yarn)
clean-cache:
    @echo "Cleaning language caches..."
    @go clean -modcache 2>/dev/null || true
    @npm cache clean --force 2>/dev/null || true
    @yarn cache clean 2>/dev/null || true
    @echo "Cache cleaned."

# Clean system trash & orphans (Requires sudo for system pkg managers)
clean-system:
    @echo "Cleaning system trash & orphans..."
    @if command -v trash-empty &> /dev/null; then trash-empty 30; fi
    @if [ -f /etc/debian_version ] && command -v apt-get &>/dev/null; then \
        sudo apt-get autoremove -y && sudo apt-get clean; \
    elif [ -f /etc/fedora-release ] && command -v dnf &>/dev/null; then \
        sudo dnf autoremove -y && sudo dnf clean all; \
    fi

# --- Secrets Management ---

# Load secrets from Bitwarden into current session (Nushell only helper)
# Usage: load-env (just secrets-load <id> | from json)
secrets-load item_id:
    @if ! command -v bw &> /dev/null; then echo "bw cli not found"; exit 1; fi
    @if [ -z "${BW_SESSION-}" ]; then echo "BW_SESSION empty. Run 'bw unlock' first."; exit 1; fi
    @bw get item {{item_id}} | jq -r '.fields | map({key: .name, value: .value}) | from_entries'

# --- Project Templates ---

# Create a new project from template
new type name:
    @nu -c "new {{type}} {{name}}"
